! Copyright 2007, by the California Institute of Technology. ALL
! RIGHTS RESERVED. United States Government Sponsorship acknowledged. Any
! commercial use must be negotiated with the Office of Technology Transfer
! at the California Institute of Technology.

! This software may be subject to U.S. export control laws. By accepting this
! software, the user agrees to comply with all applicable U.S. export laws and
! regulations. User has the responsibility to obtain export licenses, or other
! export authority as may be required before exporting such information to
! foreign countries or providing access to foreign persons.

! $Id$
      ! ------------------- ratios -----------------------
      ! subroutine ratios( array1, array2, exvalues, exratios, &
      !  & minratio, maxratio, meanratio, stddevratio, rmsratio, medianratio, &
      !  & fillValue, op )
      !  integer, parameter                             :: KINDVALUE = r4
      !  ! Args
      real(KINDVALUE), dimension(:), intent(in)      :: array1, array2
      real(KINDVALUE), dimension(2), intent(out)     :: exvalues
      real(KINDVALUE), dimension(2), intent(out)     :: exratios
      real(KINDVALUE), optional, intent(out)         :: minratio
      real(KINDVALUE), optional, intent(out)         :: maxratio
      real(KINDVALUE), optional, intent(out)         :: meanratio
      real(KINDVALUE), optional, intent(out)         :: stddevratio
      real(KINDVALUE), optional, intent(out)         :: rmsratio
      real(KINDVALUE), optional, intent(out)         :: medianratio
      real(KINDVALUE), optional, intent(in)          :: FillValue
      character, optional, intent(in)                :: op
      ! Internal variables
      integer                                        :: i
      integer, dimension(1)                          :: lmax
      real(KINDVALUE)                                :: myFillValue
      character                                      :: operator
      real(KINDVALUE)                                :: rmin, rmax, rmean, &
        & rstddev, rrms, rmedian
      real(KINDVALUE), dimension(size(array1))       :: tarray1, tarray2
      real(KINDVALUE)                                :: vmax
      real(KINDVALUE)                                :: Zero
      ! Executable
      ! 1st--no matter what, let every returned arg be given some value
      exvalues = 0._KINDVALUE
      exratios = -1._KINDVALUE
      if ( present(minratio   ) ) minratio    = -1._KINDVALUE
      if ( present(maxratio   ) ) maxratio    = -1._KINDVALUE
      if ( present(meanratio  ) ) meanratio   = -1._KINDVALUE
      if ( present(stddevratio) ) stddevratio = -1._KINDVALUE
      if ( present(rmsratio   ) ) rmsratio    = -1._KINDVALUE
      if ( present(medianratio) ) medianratio = -1._KINDVALUE
      operator = ' '
      if ( present(op) ) operator = op
      if ( size(array1) < 1 .or. size(array2) < 0 ) return
      select case (operator)
      case ('-')
        tarray1 = array1 - array2
        tarray2 = max( abs(array1), abs(array2) )
      case ('+')
        tarray1 = array1 + array2
        tarray2 = max( abs(array1), abs(array2) )
      case ('<')
        tarray1 = min( abs(array1), abs(array2) )
        tarray2 = max( abs(array1), abs(array2) )
      case default
        tarray1 = array1
        tarray2 = array2
      end select
      vmax = maxval( abs(tarray1) )
      if ( vmax <= 0._KINDVALUE ) return
      exvalues(1) = vmax
      lmax = maxloc( abs(tarray1) )
      if ( abs(tarray2(lmax(1))) /= 0._KINDVALUE ) &
        & exratios(1) = vmax / abs(tarray2(lmax(1)))
      vmax = 0._KINDVALUE
      do i=1, min( size(tarray1), size(tarray2) )
        if ( tarray2(i) == 0._KINDVALUE ) cycle
        if ( abs(tarray1(i) / tarray2(i)) > vmax ) then
          vmax = abs(tarray1(i) / tarray2(i))
          exvalues(2) = abs(tarray1(i))
          exratios(2) = vmax
        endif
      enddo
      
      if ( present(minratio) .or. present(maxratio) .or. present(meanratio) .or. &
        & present(stddevratio) .or. present(rmsratio) .or. present(medianratio) ) then
        myFillValue = -999.99
        Zero = 0.
        ! ratio = myFillValue
        where ( tarray2 /= Zero )
          ! ratio = abs( tarray1 / tarray2 )
          tarray1 = abs( tarray1 / tarray2 )
        elsewhere
          tarray1 = myFillValue
        end where
        call allstats( tarray1, min=rmin, max=rmax, mean=rmean, &
          & stddev=rstddev, rms=rrms, median=rmedian, fillValue=myFillValue )
        if ( present(minratio   ) ) minratio    = rmin
        if ( present(maxratio   ) ) maxratio    = rmax
        if ( present(meanratio  ) ) meanratio   = rmean
        if ( present(stddevratio) ) stddevratio = rstddev
        if ( present(rmsratio   ) ) rmsratio    = rrms
        if ( present(medianratio) ) medianratio = rmedian
      endif
      
      ! end subroutine ratios
!
! $Log$
! Revision 2.1  2007/07/16 23:33:59  pwagner
! First commit
!
