
! RIGHTS RESERVED. United States Government Sponsorship acknowledged. Any
! commercial use must be negotiated with the Office of Technology Transfer
! at the California Institute of Technology.

! This software may be subject to U.S. export control laws. By accepting this
! software, the user agrees to comply with all applicable U.S. export laws and
! regulations. User has the responsibility to obtain export licenses, or other
! export authority as may be required before exporting such information to
! foreign countries or providing access to foreign persons.

! $RCSFile: $
! $Id$

!     subroutine *HTVV ( MODE, LPIVOT, L1, M, U, UPARAM, C )

!        Construction and/or application of a single Householder
!     transformation..     Q = I + U*(U**T)/b
!     where I is the MxM identity matrix, b is a scalar, and U is an
!     M-dimensional Householder vector.
!        All vectors are M-vectors but only the components in positions
!     LPIVOT, and L1 through M, will be referenced.
!     ------------------------------------------------------------------
!                         Subroutine arguments

!     MODE  [in]  = 1 OR 2   When MODE = 1 this subr determines the
!           parameters for a Householder transformation and applies
!           the transformation to NVC vectors.  When MODE = 2 this
!           subr applies a previously determined Householder
!           transformation.
!     LPIVOT  [in]  The index of the pivot element.
!     L1,M  [in]  If L1 <= M  elements LPIVOT and L1 through M will
!           be referenced.  If L1 > M the subroutine returns
!           immediately.  This may be regarded
!           as performing an identity transformation.
!     U()  [inout]  Contains the "pivot" vector.  Typically U() will be
!           a rwo or column of a two-dimensional array in the calling
!           program. When MODE = 1 this is the vector from which
!           Householder parameters are to be determined. When MODE = 2
!           this is the result from previous computation with MODE = 1.
!     UPARAM  [inout]  Holds a value that supplements the contents
!           of U() to complete the definition of a
!           Householder transformation.  Computed when MODE = 1 and
!           reused when MODE = 2.
!           UPARAM is the pivot component of the Householder U-vector.
!     C()  [inout]  On entry contains one M-vector to which a
!           Householder transformation is to be applied. On exit
!           contains the transformed vector.
!     ------------------------------------------------------------------
!     Subprograms referenced: NRM2
!     ------------------------------------------------------------------
!          This code was originally developed by Charles L. Lawson and
!     Richard J. Hanson at Jet Propulsion Laboratory in 1973.  The
!     original code was described and listed in the book,

!                  Solving Least Squares Problems
!                  C. L. Lawson and R. J. Hanson
!                  Prentice-Hall, 1974

!     ------------------------------------------------------------------
      real(rk) :: B, FAC, HOLD, VNORM, SUM, BINV
      integer ::  IUL0
!     ------------------------------------------------------------------
      if ( 0 >= LPIVOT .or. LPIVOT >= L1 .or. L1 > M) return

      if ( MODE == 1 ) then
!                            ****** CONSTRUCT THE TRANSFORMATION. ******
         IUL0 = L1 - 1
         if ( IUL0 == LPIVOT ) then
            VNORM = NRM2(U(L1-1:M))
         else
            HOLD = U(IUL0)
            U(IUL0) = U(LPIVOT)
            VNORM = NRM2(U(L1-1:M))
            U(IUL0) = HOLD
         end if

         if ( U(LPIVOT) > 0.0_rk) VNORM = -VNORM
         UPARAM = U(LPIVOT)-VNORM
         U(LPIVOT) = VNORM
      end if
!            ****** Apply the transformation  I + U*(U**T)/B  to C. ****

      if ( size(c) == 0 ) return

      B = UPARAM * U(LPIVOT)
!                                 Here B <= 0.  If B  =  0., return.
      if ( B == 0.0_rk) return
      BINV = 1.0_rk / B

      SUM = UPARAM * C(LPIVOT) + DOT_PRODUCT(U(L1:M),C(L1:M))
      if ( SUM /= 0.0_rk ) then
         FAC = SUM*BINV
         C(LPIVOT) = C(LPIVOT) + FAC*UPARAM
         C(L1:M) = C(L1:M) + FAC * U(L1:M)
      end if

      return
!     end subroutine *HTVV

! $Log$
! Revision 2.1  2006/03/22 02:07:04  vsnyder
! Initial commit
!
