! Copyright 2005, by the California Institute of Technology. ALL
! RIGHTS RESERVED. United States Government Sponsorship acknowledged. Any
! commercial use must be negotiated with the Office of Technology Transfer
! at the California Institute of Technology.

! This software may be subject to U.S. export control laws. By accepting this
! software, the user agrees to comply with all applicable U.S. export laws and
! regulations. User has the responsibility to obtain export licenses, or other
! export authority as may be required before exporting such information to
! foreign countries or providing access to foreign persons.

! Implementation of arithmetic and logical operations on the type Value_T

! These are provided as an include file rather than simply providing public
! identifiers in Declaration_Table, where Value_T is defined and these
! implementations are also provided, so that an optimizing compiler might
! inline them, and then perhaps optimize away redundant assignment of type
! and units components.

! $RCSfile$

! ----------------------------------------------------  Value_Add  -----
  elemental type(value_t) function Value_Add ( V1, V2 ) result ( R )
    type(value_t), intent(in) :: V1, V2
    r = value_t(v1%what, v1%type, v1%value + v2%value, v1%units)
  end function Value_Add

! ---------------------------------------------------  Value_Diff  -----
  elemental type(value_t) function Value_Diff ( V1, V2 ) result ( R )
    type(value_t), intent(in) :: V1, V2
    r = value_t(v1%what, v1%type, v1%value - v2%value, v1%units)
  end function Value_Diff

! ----------------------------------------------------  Value_Div  -----
  elemental type(value_t) function Value_Div ( V1, V2 ) result ( R )
    type(value_t), intent(in) :: V1, V2
    r = value_t(v1%what, v1%type, v1%value / v2%value, v1%units)
  end function Value_Div

! --------------------------------------------------  Value_Equal  -----
  elemental logical function Value_Equal ( V1, V2 ) result ( R )
    type(value_t), intent(in) :: V1, V2
    r = v1%what == v2%what .and. v1%type == v2%type .and. &
      & all(v1%value == v2%value) .and. all(v1%units == v2%units)
  end function Value_Equal

! ----------------------------------------------------  Value_Neg  -----
  elemental type(value_t) function Value_Neg ( V1 ) result ( R )
    type(value_t), intent(in) :: V1
    r = value_t(v1%what, v1%type, -v1%value, v1%units)
  end function Value_Neg

! ---------------------------------------------------  Value_Prod  -----
  elemental type(value_t) function Value_Prod ( V1, V2 ) result ( R )
    type(value_t), intent(in) :: V1, V2
    r = value_t(v1%what, v1%type, v1%value * v2%value, v1%units)
  end function Value_Prod

! ---------------------------------------------------  Value_Same  -----
  elemental type(value_t) function Value_Same ( V1 ) result ( R )
    type(value_t), intent(in) :: V1
    r = value_t(v1%what, v1%type, v1%value, v1%units)
  end function Value_Same

! ------------------------------------------------  Value_Unequal  -----
  elemental logical function Value_Unequal ( V1, V2 ) result ( R )
    type(value_t), intent(in) :: V1, V2
    r = .not. ( v1 == v2 )
  end function Value_Unequal

! $Log$
! Revision 2.3  2013/12/12 01:56:25  vsnyder
! Change 'type' component to 'what' component
!
! Revision 2.2  2013/10/10 22:40:31  vsnyder
! Spell RCSfile correctly
!
! Revision 2.1  2013/10/10 22:39:31  vsnyder
! Initial commit
!
