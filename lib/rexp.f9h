! Copyright (c) 1999, California Institute of Technology.  ALL RIGHTS RESERVED.
! U.S. Government Sponsorship under NASA Contract NAS7-1407 is acknowledged.

!     function *REXP ( X ) result ( REXP )
!>> 2002-10-10 REXP  WVS    Adapted to Fortran 95
!>> 1998-10-29 SREXP Krogh  Moved external statement up for mangle.
!>> 1994-10-20 SREXP Krogh  Changes to use M77CON
!>> 1994-05-20 SREXP WVS JPL Make SP and DP alike using CHGTYP
!>> 1993-05-06 SREXP WVS JPL Convert from NSWC to Math 77
!--S replaces "?": ?REXP

! $Id$
! $RCSfile$

! ----------------------------------------------------------------------
!            EVALUATION OF THE FUNCTION EXP(X) - 1
!-----------------------------------------------------------------------

!     ROUND is the smallest number such that ROUND + 1 > 1.
      real(rk), parameter :: ROUND = epsilon(1.0_rk)
!---------------------------
      real(rk), parameter :: A0 =  .248015873015873015873016E-04_rk
      real(rk), parameter :: A1 = -.344452080605731005808147E-05_rk
      real(rk), parameter :: A2 =  .206664230430046597475413E-06_rk
      real(rk), parameter :: A3 = -.447300111094328162971036E-08_rk
      real(rk), parameter :: A4 =  .114734027080634968083920E-11_rk
!---------------------------
      real(rk), parameter :: B1 = -.249994190011341852652396E+00_rk
      real(rk), parameter :: B2 =  .249987228833107957725728E-01_rk
      real(rk), parameter :: B3 = -.119037506846942249362528E-02_rk
      real(rk), parameter :: B4 =  .228908693387350391768682E-04_rk
!---------------------------
!     CI = 1/FACTORIAL(I + 2)
!---------------------------
      real(rk), parameter :: C1 = 1.0E0/6.0E0_rk
      real(rk), parameter :: C2 = 1.0E0/24.0E0_rk
      real(rk), parameter :: C3 = 1.0E0/120.0E0_rk
      real(rk), parameter :: C4 = 1.0E0/720.0E0_rk
      real(rk), parameter :: C5 = 1.0E0/5040.0E0_rk
!---------------------------
      real(rk), parameter :: P1 = .914041914819518E-09_rk
      real(rk), parameter :: P2 = .238082361044469E-01_rk
!-----------------------
      real(rk), parameter :: Q1 =-.499999999085958E+00_rk
      real(rk), parameter :: Q2 = .107141568980644E+00_rk
      real(rk), parameter :: Q3 =-.119041179760821E-01_rk
      real(rk), parameter :: Q4 = .595130811860248E-03_rk
!-----------------------
      real(rk) :: Z

      if ( abs(x) <= 0.15e0_rk ) then
         if ( round < 1.0e-14_rk ) then

!           Z is a minimax approximation of the series

!                C6 + C7*X + C8*X**2 + ....

!           This approximation is accurate to within
!           1 unit of the 23-rd significant digit.
!           The resulting value for REXP is accurate to
!           within 1 unit of the 33-rd significant
!           digit.

            z = ((((a4*x + a3)*x + a2)*x + a1)*x + a0) /                 &
     &          ((((b4*x + b3)*x + b2)*x + b1)*x + 1.0e0_rk)
            rexp = x * (((((((z*x + c5)*x + c4)*x + c3)*x + c2)*x +      &
     &                  c1)*x + 0.5e0_rk)*x + 1.0e0_rk)
            return
         end if
         rexp = x*(((p2*x + p1)*x + 1.0e0)/((((q4*x + q3)*x + q2)*x +    &
     &           q1)*x + 1.0e0_rk))
         return
      end if

      if (x >= 0.0e0_rk ) then
         z = exp(x)
         rexp = z*(0.5e0_rk + (0.5e0_rk - 1.0e0_rk/z))
         return
      end if
      if (x >= log(round) ) then
         rexp = (exp(x) - 0.5e0_rk) - 0.5e0_rk
         return
      end if
      rexp = -1.0e0_rk
      return
!     end function *REXP

! $Log$
! Revision 2.2  2003/05/05 23:00:05  livesey
! Merged in feb03 newfwm branch
!
! Revision 2.1.2.1  2003/04/16 20:12:15  vsnyder
! Remove save variable and argument declarations
!
! Revision 2.1  2002/10/10 20:35:22  vsnyder
! Initial conversion from Math77
!
