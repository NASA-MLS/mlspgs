! Copyright 2005, by the California Institute of Technology. ALL
! RIGHTS RESERVED. United States Government Sponsorship acknowledged. Any
! commercial use must be negotiated with the Office of Technology Transfer
! at the California Institute of Technology.

! This software may be subject to U.S. export control laws. By accepting this
! software, the user agrees to comply with all applicable U.S. export laws and
! regulations. User has the responsibility to obtain export licenses, or other
! export authority as may be required before exporting such information to
! foreign countries or providing access to foreign persons.

! subroutine ?_CSPLINE ( XIN, XOUT, YIN, YOUT, NIN, NOUT, YMIN, YMAX)

! $Id$
! $RCSfile$

!  Given arrays XIN & YIN of length NIN containing a tabulated function,
!  with XIN ordered, and given the (ordered) array: XOUT of length: NOUT,
!  this subroutine Returns the array: YOUT which is the cubic-spline
!  interpolation of (XIN,YIN) computed for each: XOUT

    integer, intent(in) :: NIN, NOUT
    real(rk), intent(in) :: XIN(abs(nin)), XOUT(nout), YIN(abs(nin))
    real(rk), optional, intent(in) :: YMIN, YMAX
    real(rk), intent(out) :: YOUT(:)

!   logical, intrinsic :: ALL
    integer :: I, JIN, KHI, KLO, KPL
    real(rk) :: COEFF(4, abs(nin)), X_IN(abs(nin))
    real(rk) :: X, Y, XL, XH, YL, YH, YLIN, DYDX, H

    if ( all(yin(2:) == yin(1)) ) then
      yout = yin(1)
      return
    end if

    jin = abs(nin)

    if ( xin(jin) > xin(1) ) then
      x_in = xin
      coeff(1,:) = yin
    else
      x_in = xin(jin:1:-1)
      coeff(1,:) = yin(jin:1:-1)
    end if

    coeff(2,1) = 0.0
    coeff(2,jin) = 0.0

    call pcspl ( x_in, coeff, jin, 0, 0 )

    klo = -1
    kpl = -2
    do i = 1, nout

      x = xout(i)
      call purehunt ( x, x_in, jin, klo, khi )
      if ( klo /= kpl ) then
        kpl = klo
        xl = x_in(klo)
        xh = x_in(khi)
        yl = coeff(1,klo)
        yh = coeff(1,khi)
        dydx = (yh-yl)/(xh-xl)
      end if

      h = x - xl
      ylin = yl + dydx * h
      y = yl + h * (coeff(2,klo) + h * (coeff(3,klo) + h * coeff(4,klo) ) )

      if ( abs(y-ylin) > 0.25 * abs(ylin) ) then
        y = ylin
      else if ( present(ymin) .and. present(ymax) ) then
        if ( y < ymin  .or. y > ymax ) y = ylin
      end if

      yout(i) = y

    end do

! end subroutine ?_CSPLINE

! $Log$
! Revision 2.3  2011/08/26 17:52:29  pwagner
! purehunt recovers optimized functionality of fwdmdls own hunt
!
! Revision 2.2  2011/08/26 01:20:31  pwagner
! Fixed obvious bug
!
! Revision 2.1  2011/08/26 00:22:39  pwagner
! Moved here from fwdmdl
!
! Revision 2.9  2005/06/22 18:14:01  pwagner
! Reworded Copyright statement, moved rcs id
!
! Revision 2.8  2004/03/30 00:55:02  vsnyder
! Remove declaration for unused variable
!
! Revision 2.7  2003/11/08 02:17:30  vsnyder
! Simplification, polishing
!
! Revision 2.6  2002/12/03 01:29:21  vsnyder
! Typo in a comment
!
! Revision 2.5  2002/10/14 20:49:52  vsnyder
! Add copyright notice, replace ID block with comments, cosmetic changes
!
! Revision 2.4  2002/10/02 15:49:38  bwknosp
! Added Id and RCS info
!
