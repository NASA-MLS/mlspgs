!      subroutine SSTAT1(XTAB, NX, STATS, IHIST, NCELLS, X1, X2)
!>> 1997-04-25 SSTAT1 Krogh Simplified code (WVS suggestion)
!>> 1994-11-11 SSTAT1 Krogh Declared all vars.
!>> 1994-10-20 SSTAT1 Krogh Changes to use M77CON
!>> 1989-10-20 SSTAT1 CLL
!>> 1987-05-01 SSTAT1 Lawson  Initial code.
!--S replaces "?": ?STAT1, ?STAT2
!
!        This subr computes basic statistics for X, storing them is
!     STATS() as follows:
!
!             STATS(1) = Total count
!             STATS(2) = Min
!             STATS(3) = Max
!             STATS(4) = Mean
!             STATS(5) = Standard deviation
!
!     This subr also accumulates counts in IHIST() to develop a
!     histogram of values of X.
!
!        The data to be treated is given in XTAB(1:NX).  If the
!     value of STATS(1) on entry is positive , say = COUNT, it is
!     assumed that COUNT data values have been processed previously
!     and results from that processing are in IHIST() and STATS().
!     These results will be updated to reflect the additional set of
!     NX values.
!
!        Alternatively, if STATS(1) is zero, the initial contents of
!     STATS(2:5) and IHIST() will be ignored and results will be
!     computed just for the present data set XTAB(1:NX).
!
!        The user must specify the range and resolution of the histogram
!     by setting X1, X2, and NCELLS.  The end cells, IHIST(1) and
!     IHIST(NCELLS) will be used to count occurences of X less than X1
!     or greater than X2 respectively.
!     The cells IHIST(2) through IHIST(NCELLS-1) will
!     be used to count occurences of X in NCELLS-2 equal-length
!     subintervals of [X1, X2].
!
!        Define h = (X2 - X1)/(NCELLS-2).  X-intervals will be
!     associated with elements of IHIST() as follows.
!
!          X interval                   Counting cell
!
!          (-Infinity, X1)              IHIST(1)
!          [X1+(i-2)*h, X1+(i-1)*h)     IHIST(i), i = 2,...,NCELLS-2
!          [X2-h, X2]                   IHIST(NCELLS-1)
!          (X2, Infinity)               IHIST(NCELLS)
!
!        After use of this subroutine, the user can call
!     SSTAT2, to produce a printer-plot of the histogram and print the
!     statistics.
!
!        Remark:  It is more efficient to call this subroutine one
!     time giving it N points rather than calling it N times giving it
!     one point each time, but the results will be the same to within
!     arithmetic limitations either way.
!     ------------------------------------------------------------------
!                    Subroutine arguments
!
!     XTAB()  [in]  Array of NX values whose statistics are to be
!           computed.
!     NX     [in]  Number of values given in XTAB().
!           Require NX .ge. 1.
!     STATS()  [inout]  Array of length 5 into which statistics are or
!           will be stored.  Initial value of STATS(1) must be positive
!           if IHIST() and STATS() contain prior results that are to be
!           updated.  Otherwise the initial value of STATS(1) must be
!           zero.
!     IHIST()  [inout]  Integer array of length at least NCELLS into
!           which counts will be accumulated.
!     NCELLS   [in]  Total number of classification regions.
!           Require NCELLS .ge. 3.
!     X1,X2  [in]  Lower and upper boundaries, respectively defining
!           the range of y values to be classified into NCELLS-2 equal
!           intervals.  Require X1 < X2.
!     ------------------------------------------------------------------
!        The value of FAC is not critical.  It should be greater than
!     one.  The program does less computation each time the test
!     (abs(DELTA) .lt. TEST) is satisfied.  It will be true more
!     frequently if FAC is larger.  There is probably not much advantage
!     in setting FAC larger than 4, so 64 is probably more than ample.
!     ------------------------------------------------------------------
!     C. L. Lawson and S. Y. Chiu, JPL, Apr 1987.
!     1989-10-20 CLL Moved integer declaration earlier to avoid warning
!     msg from Cray compiler.
!     ------------------------------------------------------------------
! Copyright (c) 2004, California Institute of Technology.  ALL RIGHTS RESERVED.
! U.S. Government Sponsorship under NASA Contract NAS7-1407 is acknowledged.

! $Id$
      integer NCELLS, NX
!      integer IHIST(NCELLS)
      integer, optional, intent(inout) :: IHIST(:)
      integer I, INDEX, J
!     real             COUNT, DELTA, FAC, PREV
!     real             SCALE, RSCALE, SCLNEW, STATS(5), SUMSCL
!     real             TEMP, TEST, X, X1, X2, XMAX, XMEAN, XMIN
!     real             XTAB(NX)
!     parameter(FAC = 64.0E0)
!     ------------------------------------------------------------------
      if(NX .lt. 1) return
      COUNT = STATS(1)
      if(COUNT .eq. 0.E0) then
        if ( present(IHIST) ) then
         do 10 I=1,NCELLS
            IHIST(I) = 0
   10    continue
        endif
!
!                    Begin: Special for first point
!
         PREV = 0.E0
         X = XTAB(1)
         XMIN = X
         XMAX = X
         XMEAN = 0.E0
         TEST = -1.0E0
         SUMSCL = 0.E0
!                    End: Special for first point
!
      else
!                    Here when COUNT is > zero on entry.
         XMIN = STATS(2)
         XMAX = STATS(3)
         XMEAN = STATS(4)
!
         if(STATS(5) .eq. 0.E0) then
            TEST = -1.0E0
            SUMSCL = 0.E0
         else
!
!              STATS(5) contains the old value of Sigma.  Since it is
!              nonzero (positive) here, COUNT must be at least 2.
!
            SCALE =  STATS(5)
            RSCALE = 1.0E0/SCALE
            TEST = FAC * SCALE
            SUMSCL = COUNT - 1.0E0
         endif
      endif
!
      do 30 J = 1, NX
         PREV = COUNT
         COUNT = COUNT + 1.0E0
         X = XTAB(J)
         XMIN = min(X, XMIN)
         XMAX = max(X, XMAX)
!        .                             Begin: Tally in histogram.
         if ( .not. present(IHIST) ) then
         elseif(X .lt. X1) then
            IHIST(1) = IHIST(1) + 1
         elseif(X .gt. X2) then
            IHIST(NCELLS) = IHIST(NCELLS) + 1
         else
!                          Following stmt converts integer to float.
            TEMP = NCELLS-2
!                          Following stmt converts float to integer.
            INDEX = TEMP*(X-X1)/(X2-X1)
            IHIST(INDEX + 2) = IHIST(INDEX + 2) + 1
         endif
!        .                             End: Tally in histogram.
!
         DELTA = X - XMEAN
!
!              Expect abs(DELTA) .le. TEST most of the time.
!
         if(abs(DELTA) .gt. TEST) then
            if( DELTA .eq. 0.E0 ) go to 20
!
!                     Here  abs(DELTA) .gt. TEST  and  DELTA .ne. 0.E0
!                     Must compute new SCALE, RSCALE and TEST
!                     and update SUMSCL if it is nonzero.
!
            SCLNEW = abs(DELTA)
            RSCALE = 1.0E0 / SCLNEW
            TEST = FAC * SCLNEW
            if(SUMSCL .ne. 0.E0) SUMSCL = SUMSCL * (SCALE * RSCALE)**2
            SCALE = SCLNEW
         endif
         XMEAN = XMEAN + DELTA / COUNT
         SUMSCL = SUMSCL + (PREV/COUNT) * (DELTA*RSCALE)**2
   20    continue
   30 continue
!
      STATS(1) = COUNT
      STATS(2) = XMIN
      STATS(3) = XMAX
      STATS(4) = XMEAN
      if(PREV .eq. 0.E0) then
         STATS(5) = 0.E0
      else
         STATS(5) = SCALE * sqrt( SUMSCL / PREV )
      endif
!      return
!      end subroutine
!
! $Log$
! Revision 2.1  2004/09/13 20:40:37  pwagner
! First commit
!
