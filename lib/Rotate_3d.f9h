! subroutine Rotate_3d_* ( Original_Vector, Phi, About_Vector, Rotated_Vector )

    real(rk), intent(in) :: Original_Vector(3) ! in XYZ coordinates
    real(rk), intent(in) :: Phi                ! to rotate, Radians
    real(rk), intent(in) :: About_Vector(3)    ! in XYZ coordinates
                                               ! An unit vector parallel to it
                                               ! is constructed.
    real(rk), intent(out) :: Rotated_Vector(3) ! in XYZ coordinates

    real(rk) :: About_U(3)
    real(rk) :: CP, One_m_CP, SP ! Cos(phi), (1-cos(phi)), sin(phi)
    integer :: I, J
    real(rk) :: R(3,3) ! The rotation matrix

    ! Compute an unit vector parallel to About.
    About_U = about_vector / norm2(about_vector)

    !{ Construct the rotation matrix:
    !  $R_{ij} = \cos\phi\, \delta_{ij} + ( 1 - \cos\phi ) \, n_i n_j -
    !            \sin\phi\, \epsilon_{ijk}\, n_k$
    !  where $\phi$ is the rotation angle, $\hat{\mathbf{n}}$ is the unit
    !  vector about which the rotation occurs, $\delta_{ij}$ is the
    !  Kronecker symbol, $\epsilon_{ijk}$ is the Levi_Civita completely
    !  asymmetric symbol, and there is summation on $k$; the summand has
    !  only one nonzero element.

    cp = cos(phi)
    one_m_cp = 1.0_rk - cos(phi)
    sp = sin(phi)

    do i = 1, 3
      do j = 1, 3
        r(i,j) = d(i,j) * cp + one_m_cp * about_u(i) * about_u(j) - &
               & sp * l(i,j) * about_u(k(i,j))
      end do
    end do

    rotated_vector = matmul ( r, original_vector )

! end subroutine Rotate_3d_*

! $Id$

! $Log$
! Revision 2.2  2014/11/04 01:26:32  vsnyder
! Add the CVS comments
!
