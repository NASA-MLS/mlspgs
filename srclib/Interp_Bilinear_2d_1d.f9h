! Copyright 2005, by the California Institute of Technology. ALL
! RIGHTS RESERVED. United States Government Sponsorship acknowledged. Any
! commercial use must be negotiated with the Office of Technology Transfer
! at the California Institute of Technology.

! This software may be subject to U.S. export control laws. By accepting this
! software, the user agrees to comply with all applicable U.S. export laws and
! regulations. User has the responsibility to obtain export licenses, or other
! export authority as may be required before exporting such information to
! foreign countries or providing access to foreign persons.

! $Id$

! subroutine Interp_Bilinear_2d_1d_r* ( XOld, Xnew, YOld, YNew, Zold, Znew, &
!   & Update )

  ! Given ZOld on coordinates (XOld x YOld), interpolate to (XNew,YNew)
  ! to give ZNew.  ZOld must have shape (size(xOld),size(yOld)), while
  ! XNew, YNew and ZNew must have the same shape.

  real(rk) :: DX(2), DY(2) ! Interpolation factors.  Xi, Eta in wvs-083.
  real(rk) :: Z            ! Interpolated result
  integer, dimension(size(zNew)) :: HX, HY ! Hunt indices
  integer :: I, IX, IY
  real(rk) :: U

  u = 0.0
  if ( present(update) ) u = merge(1.0,0.0,update)

  ! Hunt for xNew, yNew in xOld, zOld
  call hunt ( xOld, xNew, hx )
  call hunt ( yOld, yNew, hy )
  if ( size(XOld) < 2 ) then
    if ( size(YOld) < 2 ) then
      ! Don't interpolate at all
      zNew(1) = u * zNew(1) + zOld(1,1)
    else
      ! 1d interpolation in y only
      do i = 1, size(zNew)
        iy = hy(i)
        dy(1) = ( yOld(iy+1) - yNew(i) ) / ( yOld(iy+1) - yOld(iy) )
        dy(2) = 1.0 - dy(1)
        z = &
          & dy(1) * zOld(1,iy)    + &
          & dy(2) * zOld(1,iy+1) 
        zNew(i) = u * zNew(i) + z
      end do
    endif
  elseif ( size(YOld) < 2 ) then
    ! 1d interpolation in x only
    do i = 1, size(zNew)
      ix = hx(i)
      dx(1) = ( xOld(ix+1) - xNew(i) ) / ( xOld(ix+1) - xOld(ix) )
      dx(2) = 1.0 - dx(1)
      z = &
        & dx(1) * zOld(ix,1)    + &
        & dx(2) * zOld(ix+1,1) 
      zNew(i) = u * zNew(i) + z
    end do
  else
    ! Interpolate in both x and y.  See wvs-083.  zOld here is z^T in wvs-083.
    do i = 1, size(zNew)
      ix = hx(i)
      iy = hy(i)
      dx(1) = ( xOld(ix+1) - xNew(i) ) / ( xOld(ix+1) - xOld(ix) )
      dx(2) = 1.0 - dx(1)
      dy(1) = ( yOld(iy+1) - yNew(i) ) / ( yOld(iy+1) - yOld(iy) )
      dy(2) = 1.0 - dy(1)
      z = &
        & dy(1) * ( dx(1) * zOld(ix,iy)   + dx(2) * zOld(ix+1,iy)   ) + &
        & dy(2) * ( dx(1) * zOld(ix,iy+1) + dx(2) * zOld(ix+1,iy+1) )
      zNew(i) = u * zNew(i) + z
    end do
  endif

! end subroutine Interp_Bilinear_2d_1d_r*

! $Log$
! Revision 1.3  2012/12/20 01:03:31  vsnyder
! Revise to be consistent with notation in wvs-083.  Hopefully, the results
! are not changed.
!
! Revision 1.2  2009/10/03 00:37:30  vsnyder
! zNew needs a subscript everywhere
!
! Revision 1.1  2006/01/05 03:27:59  vsnyder
! Initial commit
!
