! Copyright 2005, by the California Institute of Technology. ALL
! RIGHTS RESERVED. United States Government Sponsorship acknowledged. Any
! commercial use must be negotiated with the Office of Technology Transfer
! at the California Institute of Technology.

! This software may be subject to U.S. export control laws. By accepting this
! software, the user agrees to comply with all applicable U.S. export laws and
! regulations. User has the responsibility to obtain export licenses, or other
! export authority as may be required before exporting such information to
! foreign countries or providing access to foreign persons.

! $Id$

!  subroutine InterpolateArray_r* ( oldX, oldY, newX, newY, method, extrapolate, &
!    & badValue, missingRegions, dyByDx, dNewByDOld, skipNewY, IntYdX )

    character :: ExtrapolateMethod
    integer :: D, I, Ind, NoNew, NoOld
    real(rk), pointer :: My_dyByDx(:), My_IntYdX(:)
    target :: dyByDx, IntYdX

    noNew = size(newX,1)
    noOld = size(oldX,1)
    d = 1
    if ( present(second) ) d = merge(2,1,second)

    ! Special case where only one input point
    if ( noOld == 1 ) then
      extrapolateMethod="A"
      if ( present(extrapolate)) extrapolateMethod=Capitalize(extrapolate(1:1))

      if ( d == 1 ) then
        ! If extrapolating allowed or clamped, out values same as in
        if ( extrapolateMethod=="A" .or. extrapolateMethod=="C" ) then
          do ind = 1, noNew
            newY(ind,:) = oldY(1,:)
          end do
          ! Note these next two aren't totally graceful with respect to 
          ! the missingRegions flag.
          if ( present(dyByDx) ) dyByDx = 0.0_rk
        else
          ! Else extrapolation forbidden
          do ind = 1, noNew
            if ( newX(ind) == oldX(1) ) then
              newY(ind,:) = oldY(1,:)
              if ( present(dyByDx) ) dyByDx = 0.0_rk
            else
              newY(ind,:) = badValue
              if ( present(dyByDx) ) dyByDx = badValue
            end if
          end do
        end if
      else
        ! If extrapolating allowed or clamped, out values same as in
        if ( extrapolateMethod=="A" .or. extrapolateMethod=="C" ) then
          do ind = 1, noNew
            newY(:,ind) = oldY(:,1)
          end do
          ! Note these next two aren't totally graceful with respect to 
          ! the missingRegions flag.
          if ( present(dyByDx) ) dyByDx = 0.0_rk
        else
          ! Else extrapolation forbidden
          do ind = 1, noNew
            if ( newX(ind) == oldX(1) ) then
              newY(:,ind) = oldY(:,1)
              if ( present(dyByDx) ) dyByDx = 0.0_rk
            else
              newY(:,ind) = badValue
              if ( present(dyByDx) ) dyByDx = badValue
            end if
          end do
        end if
      end if
      return
    end if

    ! Calculate the coefficients
    call interpolateArraySetup ( oldX, newX, method, coeffs, extrapolate, &
      size(oldY,3-d), present(dyByDx), dNewByDOld, present(intYdX) )

    ! Do the interpolation
    if ( d == 1 ) then
      call interpolateValues ( coeffs, oldX, oldY, newX, newY, &
        & method, extrapolate, badValue, missingRegions, dyByDx, skipNewY, intYdX )
    else
      nullify ( my_dyByDx, my_intYdX )
      do i = 1, size(newY,1)
        if ( present(dyByDx) ) my_dyByDx => dyByDx(i,:)
        if ( present(intYdX) ) my_intYdX => intYdX(i,:)
        call interpolateValues ( coeffs, oldX, oldY(i,:), newX, newY(i,:), &
          & method, extrapolate, badValue, missingRegions, my_dyByDx, &
          & skipNewY, my_intYdX )
      end do
    end if

    ! Dispose of the coefficients
    call interpolateArrayTeardown ( coeffs )

! end subroutine InterpolateArray_r*

! $Log$
! Revision 1.7  2005/08/03 16:34:20  pwagner
! InterpolateArray optionally finds antiderivatives
!
! Revision 1.6  2005/06/22 20:03:55  pwagner
! Reworded Copyright statement, moved rcs id
!
! Revision 1.5  2002/11/23 03:12:53  vsnyder
! Can't use setup with noOld == 1
!
! Revision 1.4  2002/11/23 02:32:09  vsnyder
! Modifications to accomodate InterpolateScalarUsingSetup
!
! Revision 1.3  2002/11/22 23:59:10  vsnyder
! Separate setup and teardown from interpolation
!
! Revision 1.2  2002/10/04 00:46:12  vsnyder
! Move declarations of local variables here
!
! Revision 1.1  2002/09/13 18:01:33  pwagner
! First commit
!
