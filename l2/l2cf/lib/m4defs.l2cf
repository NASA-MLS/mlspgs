; ----------------------------------------------------------------
; ----------------------------------------------------------------
; ----------------------------------------------------------------
; NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE
; NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE
;
; This Level 2 Configuration File (l2cf) has been automatically
; generated using the GNU m4 macro preprocessor.  While it is
; perfectly allowable to edit this file directly, it will probably
; be more efficient to edit the `source' files that went into m4.
;
; NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE
; NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE
; ----------------------------------------------------------------
; ----------------------------------------------------------------
; ----------------------------------------------------------------
; ----------------------------------------------------------------


; --------------------------- Changing m4 syntax etc.

; These are the m4 definitions for l2cf's
; Change the quotes to braces
changequote({,})
; Insist all m4 stuff is prefixed with !
changesyntax({@!})
; Change the comment to a semicolon
!changecom(;)
; Make _ not a possible part of m4 names
!changesyntax({O_})

; --------------------------- Low level macros for loops etc.

; Define a very simple for loop prototcol
; Increment $1 from $2 to $3, doing $4
!define({forloop},
  {!pushdef({$1}, {$2})!forloopint({$1}, {$2}, {$3}, {$4})!popdef({$1})})
!define(forloopint,{!ifelse(!eval(!$1<={$3}),1,{$4{}!ifelse(!$1, {$3}, ,
    {!define({$1}, !incr(!$1))!forloopint({$1}, {$2}, {$3}, {$4})})},{})})

; This macro calls the macro that is its first argument many times,
; once for each successive argument, with that argument being an
; argument to the macro
!define({forall},{!$1($2)!ifelse($#,2,{},{!forall}($1,!shift(!shift($@))))})

; This macro makes a list separated by commas.  The items in the last
; are arguments 3 onwards, prefixed with argument 1, and postfixed
; with arguemtn 2.
!define({makelist},{$1$3$2!ifelse($#,3,{},{, !makelist}($1,$2,!shift(!shift(!shift($@)))))})

; ------------------------------- Macros for string manipulation

; This macro trims the last character (often a newline) off it's
; argument
!define(TrimLastChar, {!substr($1,0,!eval(!len($1)-1))})

; This macro is similar but trims the first and last character off,
; typically quotes.
!define(TrimFirstLastChar, {!substr($1,1,!eval(!len($1)-3))})

; This is just a newline. It is mainly used in macros that are destined
; to be called by forall macros.
!define(nl,{
})

; This macro downcases its arguments
!define(Downcase,{!translit($1,{A-Z},{a-z})})

; This macro trims the last 3 characters from the string (_HR probably)
!define(TrimLast3,{!substr($1,0,!eval(!len($1)-3))})

; ----------------------------------------- Date etc. manipulation tools

; Define a macro that just returns the first four digits of CCSDSb date.
!define(year,{!substr(}$1{,0,4)})

; This one returns the last three digits of a CCSDSb date.
!define(doy,{!substr(}$1{,5,3)})

; This macro calls a python script to add dates etc.
!define(ccsdsbadd,{!TrimLastChar(!esyscmd(!home/mlspgs/scripts/ccsdsbadd.py $1 $2))})
!define(ccsdsbtodao,{!TrimLastChar(!esyscmd(!home/mlspgs/scripts/ccsdsbtodao.py $1))})

; ---------------------------------------- Parameter=value macros

; This set of macros allows us to write other macros which take
; arguments of the form 'parameter=value'.  The first two are low level
; macros.  The main one is ProcessParVal.  One invokes this as the first
; thing in a macro that uses this facility, with a prefix (which may be
; empty) followed by $@.  The ProcessParVal will then define a set of
; macros of the form (prefix)parameter with the appropriate values.
; Note that the 'parameter' names are all converted to lowercase
; (excluding the prefix which is left alone).

; This low level macro extracts a (lower case) parameter name
!define(GetParameter,{!Downcase(!substr($1,0,!index($1,{=})))})
; This macro extracts a parameter value
!define(GetValue,{!substr($1,!incr(!index($1,{=})))})
; This is the high level macro described above
!define(ProcessParVal,{!ifelse($2,{},{},{!dnl
!define($1!GetParameter($2),!GetValue($2))!dnl
!ProcessParVal($1,!shift(!shift($@)))})})

; ------------------------------------------------ Other low level
;                                                  macros

!define(home,!TrimLastChar(!esyscmd({echo $HOME})))

!define(comment,{!ifelse($1)})
!define(M4Error,{!errprint({M4-Error:},$*,!nl)!m4exit(1)})
