! Copyright (c) 1999, California Institute of Technology.  ALL RIGHTS RESERVED.
! U.S. Government Sponsorship under NASA Contract NAS7-1407 is acknowledged.

! subroutine ?CSPLINE_DER ( XIN, XOUT, YIN, YOUT, DYOUT, NIN, NOUT, YMIN, YMAX )

!---------------------------- RCS Ident Info -------------------------------
  character (len=*), parameter :: IdParm = &
    &  "$Id$"
  character (len=len(idParm)) :: Id = idParm
  character (len=*), parameter :: ModuleName = &
    & "$RCSfile$"
!---------------------------------------------------------------------------

!  Given arrays XIN & YIN of lenght NIN containing a tabulated function,
!  with XIN ordered, and given the (ordered) array XOUT of length NOUT,
!  this subroutine Returns the array YOUT, which is the cubic-spline
!  interpolation of (XIN,YIN) computed for each XOUT, and the array DYOUT,
!  which is the derivative of YOUT w.r.t. X evaluated at XOUT.

    integer, intent(in) :: NIN, NOUT
    real(rk), intent(in) :: XIN(abs(nin)), XOUT(nout), YIN(abs(nin))
    real(rk), optional, intent(in) :: YMIN, YMAX

    real(rk), intent(out) :: YOUT(nout), DYOUT(nout)

    integer :: JIN, I, J, KLO, KHI, KPL
    real(rk) :: COEFF(4, abs(nin)), X_IN(abs(nin))
    real(rk) :: X, Y, XL, XH, YL, YH, DY, YLIN, DYDX, DP, H, TOL

    jin = abs(nin)

    if ( all(yin == 0.0) ) then
      yout = 0.0
      dyout = 0.0
      return
    end if

    if ( xin(jin) > xin(1) ) then
      x_in = xin
      coeff(1,:) = yin
    else
      x_in = xin(jin:1:-1)
      coeff(1,:) = yin(jin:1:-1)
    end if

    coeff(2,1) = 0.0
    coeff(2,jin) = 0.0

    call pcspl ( x_in, coeff, jin, 0, 0 )

    klo = -1
    kpl = -2
    do i = 1, nout

      x = xout(i)
      call hunt ( x, x_in, jin, klo, khi )
      if ( klo /= kpl ) then
        kpl = klo
        xl = x_in(klo)
        xh = x_in(khi)
        yl = coeff(1,klo)
        yh = coeff(1,khi)
        dydx = (yh-yl)/(xh-xl)
      end if

      h = x - xl
      ylin = yl + dydx * h

      y = 0.0
      dy = 0.0
      do j = 4, 1, -1
        y = (y/j)*h + coeff(j,klo)
        if ( j < 4 ) then
          dy = (dy/j)*h + coeff(j+1,klo)
        end if
      end do

! If spline is more than 'TOL' different from linear, revert back to
! linear interpolation:

      dp = 100.0 * abs(y-ylin) / (abs(ylin) + epsilon(ylin))

      if ( dp > 25.0 ) then
        y = ylin
        dy = dydx
      else if ( present(ymin) .and. present(ymax) ) then
        if ( y < ymin  .or. y > ymax ) then
          y = ylin
          dy = dydx
        end if
      end if

      yout(i) = y
      dyout(i) = dy

    end do

! end subroutine ?CSPLINE_DER

! $Log$
! Revision 2.3  2002/10/02 15:49:26  bwknosp
! Added Id and RCS info
!
