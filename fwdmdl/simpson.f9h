! Copyright (c) 1999, California Institute of Technology.  ALL RIGHTS RESERVED.
! U.S. Government Sponsorship under NASA Contract NAS7-1407 is acknowledged.

! subroutine *Simps ( F, DX, N, R )

! $Id$
! $RCSfile$

    integer(i4), intent(in) :: N

    real(rk), intent(in) :: F(*)
    real(rk), intent(in) :: DX
    real(rk), intent(out) :: R

    real(rk) :: S

    real(rk), parameter :: K13 = 1.0_rk / 3.0_rk
    real(rk), parameter :: K38 = 3.0_rk / 8.0_rk
    real(rk), parameter :: KSN = k13 + k38   ! Bndry Simpson with Newton-Cotes

    select case ( N )
    case ( :0 )                              ! Nothing to do
      r = 0.0_rk
    case ( 1 )                               ! Midpoint rule
      r = f(1) * dx
    case ( 2 )                               ! Trapezoid rule
      r = 0.5_rk * dx * (f(1) + f(2))
    case ( 3 )                               ! Simpson's rule
      r = k13 * dx * ( f(1) + 4.0_rk * f(2) + f(3) )
    case ( 4 )                               ! Newton-Cotes 3/8 rule
      r = k38 * dx * ( f(1) + 3.0_rk * ( f(2) + f(3) ) + f(4) )
    case ( 5: )
      if ( mod(n,2) /= 0 ) then              ! Parabolic rule
        s = sum(f(3:n-2:2)) ! Assume s+s cheaper than 2*s
        r = k13 * dx * ( f(1) + f(n) + 4.0_rk * sum(f(2:n-1:2)) + s + s )
      else       ! Parabolic rule with Newton-Cotes 3/8 rule on first 4 points
        s = sum(f(6:n-2:2)) ! Assume s+s cheaper than 2*s
        r = dx * (                                             &                
          & k38 * ( f(1) + 3.0_rk * ( f(2) + f(3) ) ) +        & ! Newton-Cotes 
          & ksn * f(4)   +                                     & ! Both         
          & k13 * (4.0_rk * sum(f(5:n-1:2)) + s + s + f(n) ) )   ! Parabolic
      end if
    end select
    return
! end subroutine *Simps

! $Log$
! Revision 2.3  2002/10/02 15:50:23  bwknosp
! Added Id and RCS info
!
! Revision 2.2  2002/04/18 10:46:27  zvi
! Fix a minor bug for N Odd
!
! Revision 2.1  2002/03/21 23:35:36  zvi
! Fixing minor bug & making it more efficient
!
! Revision 2.0  2001/09/17 20:26:27  livesey
! New forward model
!
! Revision 1.3  2001/06/07 23:39:32  pwagner
! Added Copyright statement
!
! Revision 1.2  2001/01/31 01:08:48  zvi
! New version of forward model
!
