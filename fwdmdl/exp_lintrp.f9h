!---------------------------------------------------------------------
!
!     SUBROUTINE ?_EXP_LINTRP(XIN,XOUT,YIN,YOUT,NIN,NOUT)
!
      INTEGER, INTENT(IN) :: NIN,NOUT
      REAL(rk), DIMENSION(*),INTENT(IN)  :: XIN,XOUT,YIN
      REAL(rk), DIMENSION(*),INTENT(OUT) :: YOUT

      INTEGER :: I,J,K,M
      REAL(rk) :: X1,X2,X,Y,Q,R
!
      REAL(rk), PARAMETER :: TINY = epsilon(X)
!
      IF(NIN < 2) THEN
        YOUT(1:NOUT) = YIN(1)
        RETURN
      ENDIF

      J = 0
      IF(XIN(NIN) <= XIN(1)) J = 1
      IF(XOUT(NOUT) <= XOUT(1)) J = J + 2
      IF(J /= 0) THEN
        PRINT *,'** ERROR IN ?_EXP_LINTRP SUBROUTINE ..'
        PRINT *,'   XIN / XOUT MUST BE MONOTONICALLY INCREASING ..'
        RETURN
      ENDIF
!
      J = 1
      K = 1
      X1 = XIN(J)
      DO WHILE(XOUT(K) < X1 .AND. K < NOUT)
        IF(ABS(XIN(2)-XIN(1)) < TINY) THEN
          Y = 0.5_rk * (YIN(1) + YIN(2))
        ELSE IF(ABS(XOUT(K)-XIN(1)) < TINY) THEN
          Y = YIN(1)
        ELSE IF(ABS(XOUT(K)-XIN(2)) < TINY) THEN
          Y = YIN(2)
        ELSE
          R = -1.0_rk
          Q = (XOUT(K) - XIN(1)) / (XIN(2) - XIN(1))
          IF(ABS(YIN(1)) > TINY) R = YIN(2) / YIN(1)
          IF(R > 0.0_rk) THEN
            Y = YIN(1) * EXP(Q*LOG(R))                ! Exponential
          ELSE
            Y = YIN(1) + (YIN(2) - YIN(1)) * Q        ! Linear
          ENDIF
        ENDIF
        YOUT(K) = Y
        K = K + 1
      END DO
!
      IF(K == NOUT) RETURN
!
      M = NIN-1
      X2 = XIN(J+1)
      DO I = K, NOUT
        X = XOUT(I)
        DO WHILE(X > X2 .AND. J < M)
          J = MIN(M,J+1)
          X2 = XIN(J+1)
        END DO
        IF(ABS(XIN(J+1)-XIN(J)) < TINY) THEN
          Y = 0.5_rk * (YIN(J) + YIN(J+1))
        ELSE IF(ABS(X-XIN(J)) < TINY) THEN
          Y = YIN(J)
        ELSE IF(ABS(X-XIN(J+1)) < TINY) THEN
          Y = YIN(J+1)
        ELSE
          R = -1.0_rk
          Q = (X - XIN(J)) / (XIN(J+1) - XIN(J))
          IF(ABS(YIN(J)) > TINY) R = YIN(J+1) / YIN(J)
          IF(R > 0.0_rk) THEN
            Y = YIN(J) * EXP(Q*LOG(R))                ! Exponential
          ELSE
            Y = YIN(J) + (YIN(J+1) - YIN(J)) * Q      ! Linear
          ENDIF
        ENDIF
        YOUT(I) = Y
      END DO
!
!     END SUBROUTINE ?_EXP_LINTRP
!
