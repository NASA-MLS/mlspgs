! Copyright (c) 1999, California Institute of Technology.  ALL RIGHTS RESERVED.
! U.S. Government Sponsorship under NASA Contract NAS7-1407 is acknowledged.

! $Id$

! pure function *_D_SINHZ_Z3 ( Z ) result ( D_SINHZ_Z3 )
  ! Compute the derivative of sinh(z)/z, divided by z, which is
  ! (z cosh(z) - sinh(z)) / z**3, taking care when |Z| is small.

  ! Write sinh(z)/z as exp(-z) (exp(2*z)-1)/z.
    complex(rk) :: Z2  ! Z**2
    real(rk) :: ZMAG   ! |Z|**2

    real(rk), parameter :: C0 = 1.0/3.0, C1 = 1.0/(2.0*5.0)
    real(rk), parameter :: C2 = 1.0/(2.0*2.0*7), C3 = 1.0/(2.0*3.0*9.0)
    real(rk), parameter :: C4 = 1.0/(2.0*4.0*11.0)
    real(rk), parameter :: CUT = 1.5_rk  ! Switch between explicit and series
    real(rk) :: COSHX, COSY, SINHX, SINY, X, Y ! Z = (X,Y)
    real(rk) :: CXCY, SXSY ! cosh x cos y, sinh x sin y

    x = real(z)
    y = aimag(z)
    zmag = x**2 + y**2

    if ( zmag > cut ) then
      coshx = cosh(x)
      cosy = cos(y)
      sinhx = sinh(x)
      siny = sin(y)
!     d_sinhz_z3 = ( z * cosh(z) - sinh(z) ) / z**3 =
!                  ( z * cos(i z) + i sin(i z) ) / z**3 =
!                  ( z * (cosh(x)*cos(y) + i sinh(x)*sin(y) )-
!                    sinh(x)*cos(y) - i cosh(x)*sin(y) ) / z**3
      cxcy = coshx * cosy
      sxsy = sinhx * siny
      d_sinhz_z3 = cmplx( x * cxcy - y * sxsy - sinhx * cosy, &
        &                 x * sxsy + y * cxcy - coshx * siny ) / z**3
    else

!{ The series for $\frac{z \cosh z - \sinh z}{z^3}$ is
!                 $\sum_{i=0}^\infty \frac{z^{2k}}{2^k k! (2k+3)!!}$, where
!                 $(2k+3)!! = 3 \cdot 5 \cdot 7 \cdot \cdot \cdot (2k+3)$.
!
! We only use the first five terms.

      z2 = z**2
      d_sinhz_z3 = ((((c4*z2 + 1.0)*c3*z2 + 1.0)*c2*z2 + 1.0)*c1*z2 + 1.0)*c0
    end if
! end function D_D_SINHZ_Z3

! $Log$
! Revision 1.1.2.1  2003/04/30 01:45:47  vsnyder
! Initial commit
!
