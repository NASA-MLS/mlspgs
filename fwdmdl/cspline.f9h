! Copyright (c) 1999, California Institute of Technology.  ALL RIGHTS RESERVED.
! U.S. Government Sponsorship under NASA Contract NAS7-1407 is acknowledged.

! subroutine ?_CSPLINE ( XIN, XOUT, YIN, YOUT, NIN, NOUT )
!
!  Given arrays XIN & YIN of lenght NIN containing a tabulated function,
!  with XIN ordered, and given the (ordered) array: XOUT of length: NOUT,
!  this subroutine Returns the array: YOUT which is the cubic-spline
!  interpolation of (XIN,YIN) computed for each: XOUT
!
    integer, intent(in) :: NIN, NOUT
    real(rk), intent(in) :: XIN(abs(nin)), XOUT(nout), YIN(abs(nin))
    real(rk), intent(out) :: YOUT(:)
!
!   logical, intrinsic :: ALL
    integer :: I, JIN, KHI, KLO, KPL
    real(rk) :: COEFF(4, abs(nin)), X_IN(abs(nin))
    real(rk) :: X, XL, XH, YL, YH, YLIN, YSPL, DYDX, DP, H
!
    if ( all(yin(2:) == yin(1)) ) then
      yout = yin(1)
      return
    end if
!
    jin = abs(nin)
!
    if (xin(jin) > xin(1)) then
      x_in(1:jin) = xin(1:jin)
      coeff(1,1:jin) = yin(1:jin)
    else
      x_in(1:jin) = xin(jin:1:-1)
      coeff(1,1:jin) = yin(jin:1:-1)
    end if
!
    coeff(2,1) = 0.0
    coeff(2,jin) = 0.0
!
    call pcspl(x_in,coeff,jin,0,0)
!
    klo = -1
    kpl = -2
    do i = 1, nout
!
      x = xout(i)
      call hunt(x,x_in,jin,klo,khi)
      if (klo /= kpl) then
        kpl = klo
        xl = x_in(klo)
        xh = x_in(khi)
        yl = coeff(1,klo)
        yh = coeff(1,khi)
        dydx = (yh-yl)/(xh-xl)
      end if
!
      h = x - xl
      ylin = yl + dydx * h
      yspl = yl + h * (coeff(2,klo) + h * (coeff(3,klo) +     &
   &         h * coeff(4,klo) / 3.0) / 2.0)
!
      h = max(abs(ylin),abs(yspl))
      dp = 100.0*abs(ylin-yspl)/(h+epsilon(ylin))
!
! If the spline is more than 25% different from linear, revert to
! linear interpolation
!
      if (dp < 25.0) then            ! spline ok
        yout(i) = yspl
      else                           ! revert to linear
        yout(i) = ylin
      end if
!
    end do
!
! end subroutine ?_CSPLINE
