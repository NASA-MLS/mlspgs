! subroutine Get_Eta_Stru_D_D ( Basis, Grid, Eta )
!   ! Compute linear interpolating coefficients from Basis to Grid. It is
!   ! assumed that Basis(:) is sorted, but it is not assumed that Grid(:)
!   ! is sorted.  It is assumed that Grid(:) changes slowly and smoothly.
!   ! It is assumed that Size(Grid) == SIze(Eta).
!   ! To interpolate from some T whose coordinates are Basis(:) to
!   ! a value corresponding to Grid(k), compute
!   ! Basis(eta(k)%l)*eta(k)%eta + Basis(eta(k)%l+1)*(1.0-eta(k)%eta).
!   ! If Grid(k) < Basis(1) then Eta(k)%L = 1 and Eta(k)%Eta = 1.0.
!   ! If Grid(k) >= Basis(size(basis)) then Eta(k)%L = size(basis-1) and
!   ! Eta(k)%Eta = 0.0.  That is, outside the range of Basis(:), constant
!   ! extrapolation is used.
!   integer, parameter :: KB = kind(0.0d0)
!   integer, parameter :: KG = kind(0.0d0)
    real(kb), intent(in) :: Basis(:)
    real(kg), intent(in) :: Grid(:)
    type(eta_d_t), intent(out) :: Eta(:)
    integer :: K, L
    l = 1
    do k = 1, size(grid)
      do ! hunt for grid(k) in Basis(:) from previous place
        if ( basis(l) <= grid(k) ) then
          if ( grid(k) < basis(l+1) ) then
            eta(k)%eta = ( basis(l+1) - grid(k) ) / &
                       & ( basis(l+1) - basis(l) )
            exit
          end if
          if ( l == size(basis,1)-1 ) then
            eta(k)%eta = 0.0
            exit
          end if
          l = l + 1
        else if ( l > 1 ) then
          l = l - 1
        else
          eta(k)%eta = 1.0
          exit
        end if
      end do ! Hunt
      eta(k)%l = l
    end do ! k
! end subroutine Get_Eta_Stru_D_D
